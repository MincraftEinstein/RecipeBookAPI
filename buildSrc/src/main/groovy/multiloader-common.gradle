plugins {
    id 'java-library'
    id 'maven-publish'
}

base {
    version = "${mod_version}-${minecraft_version}"
    archivesName = "${mod_name}-${project.name}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

repositories {
    mavenCentral()
    // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter { includeGroupAndSubgroups('org.spongepowered') }
    }
    exclusiveContent {
        forRepositories(
                maven {
                    name = 'ParchmentMC'
                    url = 'https://maven.parchmentmc.org/'
                },
                maven {
                    name = "NeoForge"
                    url = 'https://maven.neoforged.net/releases'
                }
        )
        filter { includeGroup('org.parchmentmc.data') }
    }
    maven {
        name = 'BlameJared'
        url = 'https://maven.blamejared.com'
    }
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
['apiElements', 'runtimeElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:${base.archivesName.get()}:$version")
        capability("$group:$mod_id-${project.name}-${minecraft_version}:$version")
        capability("$group:$mod_id:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}

jar {
    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_author,
                'Specification-Version' : mod_version,
                'Implementation-Title'  : project.name,
                'Implementation-Version': mod_version,
                'Implementation-Vendor' : mod_author,
                'Built-On-Minecraft'    : minecraft_version
        ])
    }

    exclude('data/recipebook_api/')
}

processResources {
    def expandProps = [
            'group'                        : project.group, //Else we target the task's group.
            'minecraft_version'            : minecraft_version,
            'max_minecraft_version'        : max_minecraft_version,
            'fabric_version'               : fabric_version,
            'fabric_api_version'           : fabric_api_version,
            'mod_version'                  : mod_version,
            'mod_name'                     : mod_name,
            'mod_author'                   : mod_author,
            'mod_id'                       : mod_id,
            'description'                  : project.description,
            'neoforge_version'             : neoforge_version,
            'neoforge_loader_version_range': neoforge_loader_version_range,
            'java_version'                 : java_version
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }

    inputs.properties(expandProps)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            version project.version
            groupId project.group.toString()
            from components.java

            pom {
                name = "${mod_name}-${project.name}"
                description = "${project.description}"
                url = 'https://github.com/MincraftEinstein/RecipeBookAPI'

                scm {
                    url = "https://github.com/MincraftEinstein/RecipeBookAPI/tree/main"
                    connection = "scm:git:git://github.com/MincraftEinstein/RecipeBookAPI.git"
                    developerConnection = "scm:git:git@github.com:MincraftEinstein/RecipeBookAPI.git"
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MincraftEinstein/RecipeBookAPI/issues'
                }
                licenses {
                    license {
                        name = 'All Rights Reserved'
                    }
                }
                developers {
                    developer {
                        id = 'mincrafteinstein'
                        name = 'MincraftEinstein'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.teamvoided.org/releases"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
